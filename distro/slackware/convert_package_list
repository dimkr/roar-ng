#!/bin/sh

# usage: convert_package_list $package_list $repository_name

awk -F ":" '
	function trimB(s) {sub(/^ */,"",s); return s} # trim beginning of string
	function printPkg() {
		namelen=split(filename,namearr,"-"); # split name by "-"
		if(namelen>3) { # if enough fields
			sub(/\..*/,"",namearr[namelen]); pkgrev=namearr[namelen] # remove ".t(gz/xz/...)" from pkgrev
			pkgver=namearr[namelen-2] # get version
			for(i=1;i<namelen-2;i++){pkgname=pkgname"-"namearr[i]} sub(/^-/, "", pkgname) # for each remaining field, add to name, then remove beginning "-"
			printf("%s|%s|%s|%s/%s|%s|\n",pkgname,pkgver,pkgrev,pkgpath,filename,pkgdesc) # print
		}
		pkgname=""; pkgver=""; pkgrev=""; pkgpath=""; filename=""; pkgdesc=""; # clear vars
	}
	{
	if($1 == "PACKAGE NAME"){filename=trimB($2);} # trim
	if($1 == "PACKAGE LOCATION"){pkgpath=trimB($2); sub(/^\.\//,"",pkgpath)} # trim, remove "./"
	if($1 == "PACKAGE DESCRIPTION"){getline pkgdesc; sub(/[^:]*:/,"",pkgdesc); pkgdesc=trimB(pkgdesc)} # remove pkgname:, trim
	if($0 == "" && filename != "") {printPkg()} # empty line, print current package
	}END{
		if(filename != "") {printPkg()} # end of file, print package if one left
	}
' "$1"
